apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

def secretProps = new Properties()
rootProject.file("secret.properties").withInputStream { secretProps.load(it) }

android {
    namespace 'io.github.tobyhs.weatherweight'
    compileSdkVersion 33
    buildToolsVersion '33.0.0'

    defaultConfig {
        applicationId "io.github.tobyhs.weatherweight"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'ACCUWEATHER_API_KEY', "\"${secretProps['accuweatherApiKey']}\""
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        local {
            initWith buildTypes.release
            signingConfig signingConfigs.debug
            versionNameSuffix '-local'
        }

        debug {
            versionNameSuffix '-debug'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.3.2"
    }


    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    final composeBom = platform('androidx.compose:compose-bom:2023.01.00')
    implementation composeBom
    testImplementation composeBom
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.runtime:runtime-livedata'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    final activityVersion = '1.6.1'
    implementation "androidx.activity:activity-compose:$activityVersion"
    implementation "androidx.activity:activity-ktx:$activityVersion"
    implementation 'androidx.annotation:annotation:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    final lifecycleVersion = '2.5.1'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"

    [
            "com.google.dagger:hilt-compiler:$daggerVersion",
    ].each {
        kapt it
        kaptTest it
    }
    implementation "com.google.dagger:hilt-android:$daggerVersion"
    testImplementation "com.google.dagger:hilt-android-testing:$daggerVersion"

    final moshiVersion = '1.13.0'
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    implementation 'io.reactivex.rxjava3:rxjava:3.0.6'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'

    implementation 'com.github.tobyhs:rxsecretary:0.2.0'
    implementation 'com.github.tobyhs:rxsecretary-android:0.2.0'

    final retrofitVersion = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"

    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'androidx.compose.ui:ui-test-junit4'
    testImplementation 'androidx.test:core:1.5.0'
    final espressoVersion = '3.5.1'
    testImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    testImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    testImplementation 'androidx.test.ext:junit:1.1.5'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.0.0'
    testImplementation 'org.mockito:mockito-core:4.6.1'
    testImplementation 'org.robolectric:robolectric:4.9'
}
