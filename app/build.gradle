plugins {
    id 'com.android.application'
    id 'com.google.dagger.hilt.android'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.android'
}

def secretProps = new Properties()
rootProject.file("secret.properties").withInputStream { secretProps.load(it) }

android {
    namespace 'io.github.tobyhs.weatherweight'
    compileSdk 34

    defaultConfig {
        applicationId "io.github.tobyhs.weatherweight"
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'ACCUWEATHER_API_KEY', "\"${secretProps['accuweatherApiKey']}\""
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        local {
            initWith buildTypes.release
            signingConfig signingConfigs.debug
            versionNameSuffix '-local'
        }

        debug {
            versionNameSuffix '-debug'
        }
    }

    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }

    kotlin {
        jvmToolchain(17)
    }

    buildFeatures {
        buildConfig true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = '1.5.7'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    final coroutinesVersion = '1.8.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"

    final composeBom = platform('androidx.compose:compose-bom:2024.08.00')
    implementation composeBom
    testImplementation composeBom
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.runtime:runtime-livedata'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    final activityVersion = '1.9.1'
    implementation "androidx.activity:activity-compose:$activityVersion"
    implementation "androidx.activity:activity-ktx:$activityVersion"
    implementation 'androidx.annotation:annotation:1.8.2'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    final lifecycleVersion = '2.8.4'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"

    [
            "com.google.dagger:hilt-compiler:$daggerVersion",
    ].each {
        ksp it
        kspTest it
    }
    implementation "com.google.dagger:hilt-android:$daggerVersion"
    testImplementation "com.google.dagger:hilt-android-testing:$daggerVersion"

    final moshiVersion = '1.15.0'
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    ksp "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    implementation 'io.reactivex.rxjava3:rxjava:3.0.6'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'

    implementation 'com.github.tobyhs:rxsecretary:0.2.0'
    implementation 'com.github.tobyhs:rxsecretary-android:0.2.0'

    final retrofitVersion = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"

    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'androidx.compose.ui:ui-test-junit4'
    testImplementation 'androidx.test:core:1.6.1'
    final espressoVersion = '3.6.1'
    testImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    testImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    testImplementation 'androidx.test.ext:junit:1.2.1'

    final accompanistVersion = '0.30.0'
    testImplementation "com.google.accompanist:accompanist-testharness:$accompanistVersion"
    testImplementation 'io.mockk:mockk:1.13.9'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.11.1'
}
