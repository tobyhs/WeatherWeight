apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "io.github.tobyhs.weatherweight"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        local {
            initWith buildTypes.release
            signingConfig signingConfigs.debug
            versionNameSuffix '-local'
        }

        debug {
            versionNameSuffix '-debug'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    final supportLibVersion = '28.0.0'
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:support-media-compat:$supportLibVersion"
    implementation "com.android.support:support-v4:$supportLibVersion"

    final butterKnifeVersion = '8.8.1'
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"
    implementation "com.jakewharton:butterknife:$butterKnifeVersion"

    implementation 'com.prokkypew:clearrecycleradapter:1.1.0'

    final daggerVersion = '2.18'
    [
            "com.google.dagger:dagger-compiler:$daggerVersion",
            "com.google.dagger:dagger-android-processor:$daggerVersion",
    ].each {
        annotationProcessor it
        testAnnotationProcessor it
    }
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"

    final autoValue = 'com.google.auto.value:auto-value:1.5.2'
    compileOnly autoValue
    annotationProcessor autoValue

    implementation 'com.google.code.gson:gson:2.8.2'

    final gsonValueVersion = '0.9'
    annotationProcessor "me.tatarka.gsonvalue:gsonvalue-processor:$gsonValueVersion"
    implementation "me.tatarka.gsonvalue:gsonvalue:$gsonValueVersion"

    final mosbyVersion = '3.1.0'
    implementation "com.hannesdorfmann.mosby3:mvp:$mosbyVersion"
    implementation "com.hannesdorfmann.mosby3:viewstate:$mosbyVersion"
    implementation "com.hannesdorfmann.mosby3:mvp-lce:$mosbyVersion"

    implementation 'io.reactivex.rxjava2:rxjava:2.2.4'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    implementation 'com.github.tobyhs:rxsecretary:0.1.0'
    implementation 'com.github.tobyhs:rxsecretary-android:0.1.0'

    final retrofitVersion = '2.2.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.11.0'
    testImplementation 'org.robolectric:robolectric:3.8'
}
