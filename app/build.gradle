plugins {
    alias libs.plugins.android.application
    alias libs.plugins.hilt.android
    alias libs.plugins.kotlin.android
    alias libs.plugins.ksp
}

def secretProps = new Properties()
rootProject.file("secret.properties").withInputStream { secretProps.load(it) }

android {
    namespace 'io.github.tobyhs.weatherweight'
    compileSdk 34

    defaultConfig {
        applicationId "io.github.tobyhs.weatherweight"
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'ACCUWEATHER_API_KEY', "\"${secretProps['accuweatherApiKey']}\""
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        local {
            initWith buildTypes.release
            signingConfig signingConfigs.debug
            versionNameSuffix '-local'
        }

        debug {
            versionNameSuffix '-debug'
        }
    }

    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }

    kotlin {
        jvmToolchain(17)
    }

    buildFeatures {
        buildConfig true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = '1.5.7'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation libs.kotlin.stdlib

    implementation libs.coroutines.android
    testImplementation libs.coroutines.test

    final composeBom = platform(libs.compose.bom)
    implementation composeBom
    testImplementation composeBom
    implementation libs.compose.material3
    implementation libs.compose.runtime.livedata
    implementation libs.compose.ui.tooling.preview
    debugImplementation libs.compose.ui.test.manifest
    debugImplementation libs.compose.ui.tooling

    implementation libs.activity.compose
    implementation libs.activity.ktx
    implementation libs.androidx.annotation
    implementation libs.appcompat
    implementation libs.lifecycle.livedata.ktx
    implementation libs.lifecycle.viewmodel.ktx

    ksp libs.hilt.compiler
    kspTest libs.hilt.compiler
    implementation libs.hilt.android
    testImplementation libs.hilt.android.testing

    implementation libs.moshi
    ksp libs.moshi.kotlin.codegen

    implementation libs.retrofit
    implementation libs.retrofit.converter.moshi
    ksp libs.retrofit.responseTypeKeeper

    testImplementation libs.androidx.core.testing
    testImplementation libs.compose.ui.test.junit4
    testImplementation libs.androidx.test.core
    testImplementation libs.espresso.core
    testImplementation libs.espresso.intents
    testImplementation libs.androidx.junit

    testImplementation libs.accompanist.testharness
    testImplementation libs.mockk
    testImplementation libs.junit
    testImplementation libs.robolectric
}
